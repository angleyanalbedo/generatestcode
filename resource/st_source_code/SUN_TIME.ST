FUNCTION RAD:REAL
	VAR_INPUT
		X:	REAL;
	END_VAR

	RAD := MODR(0.0174532925199433 * X , 6.283185307179586476);
END_FUNCTION

FUNCTION _REAL_TO_UDINT:UDINT
	
	VAR_INPUT
		X :	REAL;
	END_VAR

	IF REAL_TO_UDINT(0.5) = UDINT#1 THEN
		_REAL_TO_UDINT := REAL_TO_UDINT(x);
	ELSIF X > 0.0 THEN
		_REAL_TO_UDINT := REAL_TO_UDINT(x+0.5); 
	ELSIF X < 0.0 THEN 
		_REAL_TO_UDINT := REAL_TO_UDINT(x-0.5); 
	ELSE 
		_REAL_TO_UDINT := UDINT#0;
  	END_IF;
END_FUNCTION

FUNCTION HOUR_TO_TIME:TIME

	VAR_INPUT
		IN :	REAL;
	END_VAR

	hour_to_time := UDINT_TO_TIME(_REAL_TO_UDINT(IN * 3600000.0));
END_FUNCTION

FUNCTION TIME_TO_UDINT2:UDINT
	VAR_INPUT
		X :	TIME;
	END_VAR

	TIME_TO_UDINT2 := DINT_TO_UDINT(TIME_TO_DINT(X));
END_FUNCTION

FUNCTION FLOOR2 : DINT
	VAR_INPUT
		x : REAL;
	END_VAR

	FLOOR2 := REAL_TO_DINT(X);
	IF DINT_TO_REAL(FLOOR2) > X THEN
		FLOOR2 := FLOOR2 - DINT#1;
	END_IF;
END_FUNCTION

FUNCTION MODR:REAL

	VAR_INPUT
		IN :	REAL;
		DIVI :	REAL;
	END_VAR


	IF divi = 0.0 THEN
		MODR := 0.0;
	ELSE
		MODR := in - DINT_TO_REAL(FLOOR2(in / divi)) * divi;
	END_IF;

END_FUNCTION

FUNCTION DEG:REAL

	VAR_INPUT
		X :	REAL;
	END_VAR

	DEG := MODR(57.29577951308232 * X, 360.0);
END_FUNCTION

FUNCTION HOUR_TO_TOD:UDINT
	VAR_INPUT
		IN :	REAL;
	END_VAR

	hour_to_tod := _REAL_TO_UDINT(IN * 3600000.00);
END_FUNCTION

FUNCTION DAY_OF_YEAR:INT
	VAR_INPUT
		IDATE :	DATE;
	END_VAR

	DAY_OF_YEAR := UDINT_TO_INT((DATE_TO_UDINT(idate) / UDINT#86400) MOD UDINT#1461);
	IF DAY_OF_YEAR > 729 THEN
		IF DAY_OF_YEAR > 1095 THEN DAY_OF_YEAR := DAY_OF_YEAR - 1095; ELSE DAY_OF_YEAR := DAY_OF_YEAR - 729; END_IF;
	ELSIF DAY_OF_YEAR > 364 THEN
		DAY_OF_YEAR := DAY_OF_YEAR - 364;
	ELSE
		DAY_OF_YEAR := DAY_OF_YEAR + 1;
	END_IF;
END_FUNCTION

FUNCTION SUN_MIDDAY:UDINT
	VAR_INPUT
		LON :	REAL;
		UTC :	DATE;
	END_VAR

	VAR
		T :	REAL;
		OFFSET :	REAL;
	END_VAR

	T := INT_TO_REAL(DAY_OF_YEAR(utc));
	OFFSET := -0.1752 * SIN(0.033430 * T + 0.5474) - 0.1340 * SIN(0.018234 * T - 0.1939);
	SUN_MIDDAY := HOUR_TO_TOD(12.0 - OFFSET - lon * 0.0666666666666);
END_FUNCTION

FUNCTION_BLOCK SUN_TIME_Block

	VAR_INPUT
		LATITUDE :	REAL;
		LONGITUDE :	REAL;
		UTC :	DATE;
		H :	REAL := -0.83333333333;
	END_VAR

	VAR_OUTPUT
		MIDDAY :	UDINT;
		SUN_RISE :	UDINT;
		SUN_SET :	UDINT;
		SUN_DECLINATION :	REAL;
	END_VAR

	VAR
		DK :	REAL;
		delta :	UDINT;
		B :	REAL;
	END_VAR

	B := latitude * 0.0174532925199433;
	MIDDAY := SUN_MIDDAY(longitude, utc);
	DK := 0.40954 * SIN(0.0172 * (INT_TO_REAL(DAY_OF_YEAR(utc)) - 79.35));
	sun_declination := DEG(DK);
	IF sun_declination > 180.0 THEN sun_declination := sun_declination - 360.0; END_IF;
	sun_declination := 90.0 - LATITUDE + sun_declination;
	delta := TIME_TO_UDINT2(HOUR_TO_TIME(ACOS((SIN(RAD(H)) - SIN(B) * SIN(DK)) / (COS(B) * COS(DK))) * 3.819718632));
	sun_rise := MIDDAY - delta;
	sun_set := MIDDAY + delta;
END_FUNCTION_BLOCK

PROGRAM program0
  VAR
	LATITUDE :	REAL;
	LONGITUDE :	REAL;
	UTC :	DATE;
	H :	REAL := -0.83333333333;
	MIDDAY :	UDINT;
	SUN_RISE :	UDINT;
	SUN_SET :	UDINT;
	SUN_DECLINATION :	REAL;
	func_block : SUN_TIME_Block;
  END_VAR
	func_block.LATITUDE := LATITUDE;
	func_block.LONGITUDE := LONGITUDE;
	func_block.UTC := UTC;
	func_block.H := H;
	func_block();
	MIDDAY := func_block.MIDDAY;	
	SUN_RISE := func_block.SUN_RISE;
	SUN_SET := func_block.SUN_SET;
	SUN_DECLINATION := func_block.SUN_DECLINATION;


END_PROGRAM

CONFIGURATION Config0

	RESOURCE Res0 ON PLC
		TASK task0(INTERVAL := T#20ms,PRIORITY := 0);
		PROGRAM instance0 WITH task0 : program0;
	END_RESOURCE
	
END_CONFIGURATION